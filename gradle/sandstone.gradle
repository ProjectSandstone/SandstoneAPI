// Shared gradle config

// IDE
apply plugin: 'idea'
apply plugin: 'eclipse'

// Programming Language
apply plugin: 'kotlin'
apply plugin: "org.jetbrains.kotlin.kapt"
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'net.ltgt.apt'
apply plugin: 'java'

// License
apply plugin: 'license'

// Shade
apply plugin: 'com.github.johnrengelman.shadow'

license {
    header rootProject.file("LICENSE_HEADER")
}

sourceCompatibility = 1.8

// Tasks
jar {
    from "$rootProject.rootDir/LICENSE"

    manifest {
        attributes 'Implementation-Title': baseName,
                'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url 'https://repo.spongepowered.org/maven/' }
    maven { url 'https://dl.bintray.com/kotlin/dokka' }
}

dependencies {

    // Dependency injection
    implementation "com.google.inject:guice:$guice_version"
    implementation "com.google.guava:guava:$guava_version"

    // Utilities
    implementation "com.github.JonathanxD:JwIUtils:$jwiutils_version"
    implementation "com.github.JonathanxD:KWCommands:$kwcommands_version"
    implementation "com.github.JonathanxD:KoresProxy:$koresproxy_version"
    implementation "com.github.koresframework:EventSys:$eventsys_version"
    implementation "com.google.code.findbugs:jsr305:$jsr305_version"
    implementation "io.github.lukehutch:fast-classpath-scanner:$fastclasspathscanner_version"

    // Logging
    implementation "org.slf4j:slf4j-api:1.7.9"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.spongepowered:math:$spongemath_version"

    //apt "com.github.ProjectSandstone:EventSys:$eventsys_version"
    kapt "com.github.ProjectSandstone:EventSys:$eventsys_version"

    // Test
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    // JUnit
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

sourceSets.configureEach { sourceSet ->
    tasks.named(sourceSet.compileJavaTaskName).configure {
        options.annotationProcessorGeneratedSourcesDirectory = file("$buildDir/generated/sources/annotationProcessor/java/${sourceSet.name}")
    }
}

/*
compileJava {
    source += sourceSets.apt.java
}
*/

sourceSets {
    main {
        java {
            srcDirs "build/generated/source/kapt/main"
        }
    }
}

test {
    useJUnit()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    classifier = 'shaded'
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    configuration {
        includes = ['src/main/Module.md']
    }
}

kapt {
    correctErrorTypes = true
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}

license {
    ext.name = project.name
    ext.description = project.description
    ext.organization = project.organization
    ext.url = project.url
    ext.year = Calendar.getInstance().get(Calendar.YEAR)

    exclude "**/*.info"
    exclude "**/*.md"
    exclude "res/**"
    exclude "src/main/resources/**"
    exclude "src/test/resources/**"
    exclude "**/SandstoneEventFactory.java"

    header rootProject.file('LICENSE_HEADER')

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'SLASHSTAR_STYLE'
        kt = 'SLASHSTAR_STYLE'
    }
}

